program sandstone;
//put buckets of sand in first bank position
//put giant seaweed in second bank slot
//Shift-rightclick, set "swap left click" for buckets to "withdraw 18"
//Set molten glass to deposit all
//Put astrals in last inv slot
//open spellbook
//mouse over a bank and hit PAUSE, will use mouse coords for bank
//
//color of molten glass in inv (573,224) line 221
//color of empty spot in inv (573,224) line 232
//color of 18th bucket of sand in inv (620,368) line 243
//color of 3rd seaweed (579,407) line 254
//color of superglass make spell in spellbook (560,322) line 265
//
//currently needs a molten glass in inv to get started. need to fix...

Type STATES = (OpenBank, DepositGlass, Sand, Seaweed, CloseBank, Cast, Logout);
Type TFuncNoArgsBool = function():Boolean;
Type state = record
    action : TFuncNoArgsBool;        //Function that defines the action/behavior
    verification : TFuncNoArgsBool;  //Function that verifies we're in the correct state
    nextState : STATES;              //State we should transition to upon success
    failedState : STATES;            //State we transition to upon failure
    gracePeriod : Integer;           //How long to wait for success to happen
    deadTime : Integer;              //How long to wait after success before performing action
    deadTimeVariance : Integer;      //Variance for dead time
  end;
Type statesArrayType = Array[0..Ord(High(STATES))] of state;
Var statesArray: statesArrayType;
function fcn(AFunction: TFuncNoArgsBool): Boolean;
begin
  result := AFunction()
end;

var
mouseTarX,mouseTarY,mouseTarTime,mouseStartTime,mouseXDelta,mouseYDelta,
mouseClicksLeft,mouseClickStart,mouseClickUntil,mouseClickDelay:Integer = 0;
mouseIsDown:Boolean = false;

hotkeysPause:Integer = 19; //pause script
hotkeysEscape:Integer = 27;//escape, close window or go to logout tab
hotkeysEnd:Integer = 35;   //stop script
hotkeysHome:Integer = 36;  //Logout
hotkeysPauseLatch,hotkeysHomeLatch,hotkeysPauseRequested:Boolean = false;


currentTime,verifiedTime,nextActionTime,logoutDetectedTime:Integer = 0;
verificationLimit:Integer = 10000;
done,verified,wasPaused,stateFound,logoutPossible:Boolean = false;
currentState,st:STATES;

bankX,bankY:Integer = 0;

procedure Log(msg:String);
begin
  Write(FormatDateTime('hh:mm:ss',Now()) + ' : ');
  Write(currentState);
  WriteLn(': ' + msg);
end;

function variance(num:Integer;varianceAmount:Integer):Integer;
begin
  result := num - varianceAmount + RandomRange(0,varianceAmount) + RandomRange(0,varianceAmount);
end;

procedure MoveAndClick(X:Integer; Y:Integer; NumClicks:Integer);
var mouseCurrentX,mouseCurrentY:Integer;
begin
  //Get the current mouse position, calculate the delta the mouse needs to move
  //by. Then set the amount of time it should take to reach the destination,
  //as well as how long after it reaches the destination before clicking.
  GetMousePos(mouseCurrentX,mouseCurrentY);
  mouseTarX := X;
  mouseTarY := Y;
  mouseXDelta := mouseTarX - mouseCurrentX;
  mouseYDelta := mouseTarY - mouseCurrentY;
  mouseClicksLeft := NumClicks;
  mouseStartTime := GetTimeRunning();
  mouseTarTime := mouseStartTime + variance(220,80);
  mouseClickDelay := mouseTarTime + variance(150,30);
end;

procedure HandleMouse;
var percent: Double;
begin
  //if we haven't reached the target time, then mouse should be in middle
  //of a movement
  if(currentTime < mouseTarTime) then
  begin
    percent := 1 - ((currentTime - mouseStartTime) / (mouseTarTime - mouseStartTime));
    MoveMouse(Round(mouseTarX - (percent * mouseXDelta)),Round(mouseTarY - (percent * mouseYDelta)));
  end else
  //otherwise, we're not in the middle of a movement, and handle any clicks
  begin
    MoveMouse(mouseTarX,mouseTarY);
    //if there are clicks left to perform, and we haven't passed the delay
    //then wait until the delay has passed, and then perform a click
    if((mouseClicksLeft > 0) and (mouseClickDelay < currentTime)) then
    begin
      HoldMouse(mouseTarX,mouseTarY,mouse_Left);
      mouseClickUntil := currentTime + variance (100,30);
      mouseClickDelay := mouseClickUntil + variance (100,30);
      mouseClicksLeft := mouseClicksLeft - 1;
      mouseIsDown := true;
    end else if((mouseIsDown = true) and (mouseClickUntil < currentTime)) then
    //wait until we've held down the mouse for long enough, then release
    begin
      ReleaseMouse(mouseTarX,mouseTarY,mouse_Left);
      mouseIsDown := false;
    end;
  end;
end;

procedure CheckHotkeys;
begin
  //Check if script end is requested by pressing "end" key
  if(IsKeyDown(hotkeysEnd)) then
  begin
    Log('END key pressed. Exiting script.');
    Halt();
  end;

  //Check if script should be paused
  if(IsKeyDown(hotkeysPause) and (hotkeysPauseLatch = false)) then
  begin
    hotkeysPauseLatch := true;
    if(hotkeysPauseRequested) then
    begin
      Log('PAUSE key pressed. Resuming script.');
      GetMousePos(bankX,bankY);
      Log('Setting bank coords to ' + IntToStr(bankX) + ',' + IntToStr(bankY));
      hotkeysPauseRequested := false;
    end else
    begin
      Log('PAUSE key pressed. Pausing until PAUSE is pressed again.');
      hotkeysPauseRequested := true;
    end;
  end;
  //Unlatch Pause (prevents multiple pause/unpause situations when holding key for a long time)
  if(hotkeysPauseLatch and (IsKeyDown(hotkeysPause) = false)) then
  begin
    hotkeysPauseLatch := false;
  end;

  //Check if logout button pressed
  if(IsKeyDown(hotkeysHome) and (hotkeysHomeLatch = false)) then
  begin
    hotkeysHomeLatch := true;
    Log('HOME key pressed. Attempting logout and pausing.');
    //Logout();
    hotkeysPauseRequested := true;
  end;
  //Unlatch logout
  if(hotkeysHomeLatch and (IsKeyDown(hotkeysHome) = false)) then
  begin
    hotkeysHomeLatch := false;
  end;
end;

function isLoggedIn():Boolean;
begin
  //Check the pixel in the timer. Wait at least 100ms after it changes before
  //assuming we're logged out (for some reason level ups cause it to flash white
  //for less than a frame, which we can catch)
  result := true;
  if(GetColor(446,488) = 1908328) then
  begin
    logoutPossible := false;
  end else
  begin
    if(logoutPossible) then
    begin
      if(currentTime > (logoutDetectedTime + 100))
      then
      begin
        result := false;
      end;
    end else
    begin
      logoutPossible := true;
      logoutDetectedTime := currentTime;
    end;
  end;
end;
function isRunning():Boolean;
begin
  result := GetColor(568,128) = 6806252;
end;
function isRunEnergyMaxed():Boolean;
begin
  result := GetColor(534,131) = 65280;
end;

function isBackpackFull():Boolean;
begin
  result := GetColor(703,447) <> 3489352;
end;



function OpenBankAction():Boolean;
begin
  MoveAndClick(variance(bankX,8),variance(bankY,8), 1);
  result:=true;
end;
function OpenBankVerification:Boolean;
var tempX,tempY:Integer;
begin
  result:=true;//no way to verify, we're on magic tab
end;

function DepositGlassAction():Boolean;
begin
  MoveAndClick(variance(578,6),variance(229,6), 1);
  result:=true;
end;
function DepositGlassVerification:Boolean;
var tempX,tempY:Integer;
begin
  result:=FindColor(tempX,tempY,3496900,572,223,574,225);
end;

function SandAction():Boolean;
begin
  MoveAndClick(variance(87,6),variance(99,6), 1);
  result:=true;
end;
function SandVerification:Boolean;
var tempX,tempY:Integer;
begin
  result:=FindColor(tempX,tempY,3489865,572,223,574,225);
end;

function SeaweedAction():Boolean;
begin
  MoveAndClick(variance(137,6),variance(97,6), 3);
  result:=true;
end;
function SeaweedVerification:Boolean;
var tempX,tempY:Integer;
begin
  result:=FindColor(tempX,tempY,3630712,619,367,621,369);
end;

function CloseBankAction():Boolean;
begin
  MoveAndClick(variance(486,5),variance(22,5), 1);
  result:=true;
end;
function CloseBankVerification:Boolean;
var tempX,tempY:Integer;
begin
  result:=FindColor(tempX,tempY,2639362,578,406,580,408);
end;

function CastAction():Boolean;
begin
  MoveAndClick(variance(563,5),variance(326,5), 1);
  result:=true;
end;
function CastVerification:Boolean;
var tempX,tempY:Integer;
begin
  result:=FindColor(tempX,tempY,7225827,559,321,561,323);
end;

function LogoutAction():Boolean;
begin
  PressKey(27);//ESC
  Wait(variance(400,175));
  PressKey(27);//Press for a second time, in case other menu is open
  MoveAndClick(variance(641,70),variance(431,17), 1);
  result := true;
end;
function LogoutVerification():Boolean;
begin
  result := isLoggedIn();
end;

begin
  //define states
  statesArray[Ord(OpenBank)].action := @OpenBankAction;
  statesArray[Ord(OpenBank)].verification := @OpenBankVerification;
  statesArray[Ord(OpenBank)].nextState := DepositGlass;
  statesArray[Ord(OpenBank)].failedState := Logout;
  statesArray[Ord(OpenBank)].gracePeriod := 3600;
  statesArray[Ord(OpenBank)].deadTime := 600;
  statesArray[Ord(OpenBank)].deadTimeVariance := 250;

  statesArray[Ord(DepositGlass)].action := @DepositGlassAction;
  statesArray[Ord(DepositGlass)].verification := @DepositGlassVerification;
  statesArray[Ord(DepositGlass)].nextState := Sand;
  statesArray[Ord(DepositGlass)].failedState := Logout;
  statesArray[Ord(DepositGlass)].gracePeriod := 1000;
  statesArray[Ord(DepositGlass)].deadTime := 500;
  statesArray[Ord(DepositGlass)].deadTimeVariance := 150;

  statesArray[Ord(Sand)].action := @SandAction;
  statesArray[Ord(Sand)].verification := @SandVerification;
  statesArray[Ord(Sand)].nextState := Seaweed;
  statesArray[Ord(Sand)].failedState := Logout;
  statesArray[Ord(Sand)].gracePeriod := 1000;
  statesArray[Ord(Sand)].deadTime := 600;
  statesArray[Ord(Sand)].deadTimeVariance := 250;

  statesArray[Ord(Seaweed)].action := @SeaweedAction;
  statesArray[Ord(Seaweed)].verification := @SeaweedVerification;
  statesArray[Ord(Seaweed)].nextState := CloseBank;
  statesArray[Ord(Seaweed)].failedState := Logout;
  statesArray[Ord(Seaweed)].gracePeriod := 1000;
  statesArray[Ord(Seaweed)].deadTime := 600;
  statesArray[Ord(Seaweed)].deadTimeVariance := 250;

  statesArray[Ord(CloseBank)].action := @CloseBankAction;
  statesArray[Ord(CloseBank)].verification := @CloseBankVerification;
  statesArray[Ord(CloseBank)].nextState := Cast;
  statesArray[Ord(CloseBank)].failedState := Logout;
  statesArray[Ord(CloseBank)].gracePeriod := 1000;
  statesArray[Ord(CloseBank)].deadTime := 600;
  statesArray[Ord(CloseBank)].deadTimeVariance := 250;

  statesArray[Ord(Cast)].action := @CastAction;
  statesArray[Ord(Cast)].verification := @CastVerification;
  statesArray[Ord(Cast)].nextState := OpenBank;
  statesArray[Ord(Cast)].failedState := Logout;
  statesArray[Ord(Cast)].gracePeriod := 1000;
  statesArray[Ord(Cast)].deadTime := 600;
  statesArray[Ord(Cast)].deadTimeVariance := 250;

  statesArray[Ord(Logout)].action := @LogoutAction;
  statesArray[Ord(Logout)].verification := @LogoutVerification;
  statesArray[Ord(Logout)].nextState := Logout;
  statesArray[Ord(Logout)].failedState := Logout;
  statesArray[Ord(Logout)].gracePeriod := 5000;
  statesArray[Ord(Logout)].deadTime := 650;
  statesArray[Ord(Logout)].deadTimeVariance := 250;

  //initial state
  currentState := OpenBank;
  GetMousePos(mouseTarX,mouseTarY);
  wasPaused := true;
  hotkeysPauseRequested := true;
  Log('Script starting paused - press PAUSE key to start');

  repeat
    currentTime := GetTimeRunning();
    CheckHotKeys();
    if(hotkeysPauseRequested = false) then
    begin
      HandleMouse();

      //if just unpaused, figure out what state we're in
      //
      //should probably rewrite this to proceed through the steps following the
      //logical order, rather than looping through all possible states in case
      //we use some for branching
      if(wasPaused) then
      begin
        Log('Unpaused - verifying state before proceeding');
        stateFound := false;
        wasPaused := false;
        for st := Low(STATES) to High(States) do
        begin
          if(stateFound = false) then
          begin
            if(statesArray[Ord(st)].verification) then
            begin
              stateFound := true;
              currentState := st;
              Log('State verification succeeded, assuming this is the current state');
            end else
            begin
              Log('State ' + IntToStr(Ord(st)) + ' verification failed, trying next state');
            end;
          end;
        end;
      end;

      //main loop here
      if(verified = false) then
      begin
        if(currentTime > (nextActionTime + statesArray[Ord(currentState)].gracePeriod)) then
        begin
          if(statesArray[Ord(currentState)].verification) then
          begin
            verified := true;
            nextActionTime := currentTime + variance(statesArray[Ord(currentState)].deadTime, statesArray[Ord(currentState)].deadTimeVariance);
            Log('State was verified. Performing action at ' + IntToStr(nextActionTime));
          end;
          if(currentTime > (nextActionTime + statesArray[Ord(currentState)].gracePeriod + verificationLimit)) then
          begin
            Log('Exceeded time limit. Setting state to failed state');
            currentState := statesArray[Ord(currentState)].failedState;
            nextActionTime := currentTime + variance(statesArray[Ord(currentState)].deadTime, statesArray[Ord(currentState)].deadTimeVariance);
            verified := false;
          end;
        end;
        //end;
      end else
      begin
        if(currentTime > nextActionTime) then
        begin
          if(statesArray[Ord(currentState)].action) then
          begin
            //action was successful
            Log('Action successful. Proceeding to next state.');
            currentState := statesArray[Ord(currentState)].nextState;
            verified := false;
          end else
          begin
            //action was not successful
            Log('Action FAILED. Setting state to failed state');
            currentState := statesArray[Ord(currentState)].failedState;
            verified := false;
          end;
        end;
      end;

    end else
    begin
      //we are currently paused
      wasPaused := true;
    end;
  until(isLoggedIn() = false);
  Log('Logout detecting.');
end.
